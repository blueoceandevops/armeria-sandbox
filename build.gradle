buildscript {
    ext {
        thriftGradlePluginVersion = "0.4.0"

        springBootVersion = "2.1.0.M4"
        braveVersion = "5.2.0"
        armeriaVersion = "0.72.0"
        thriftVersion = "0.11.0"
        retrofitVersion = "2.4.0"
        rxJava2Jdk8InteropVersion = "0.3.2"

        junitVersion = "5.3.1"
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:${thriftGradlePluginVersion}"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group = "info.matsumana.armeria"
    version = "0.1.0"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    dependencyManagement {
        imports {
            mavenBom "com.linecorp.armeria:armeria-bom:${armeriaVersion}"
            mavenBom "io.zipkin.brave:brave-bom:${braveVersion}"
            mavenBom "org.junit:junit-bom:${junitVersion}"
        }
        dependencies {
            // To override version which Armeria use
            dependency "org.springframework.boot:spring-boot-starter:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

            dependency "org.apache.thrift:libthrift:${thriftVersion}"
            dependency "com.squareup.retrofit2:converter-scalars:${retrofitVersion}"
            dependency "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
            dependency "com.squareup.retrofit2:adapter-java8:${retrofitVersion}"
            dependency "com.github.akarnokd:rxjava2-jdk8-interop:${rxJava2Jdk8InteropVersion}"
        }
    }

    dependencies {
        testCompile("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "junit", module: "junit"
        }
        testCompile "org.assertj:assertj-core"
        testCompile "org.junit.jupiter:junit-jupiter-api"
        testCompile "org.junit.jupiter:junit-jupiter-params"
        testRuntime "org.junit.jupiter:junit-jupiter-engine"
    }

    compileJava {
        options.compilerArgs += "-parameters"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}
